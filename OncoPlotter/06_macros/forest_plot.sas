/*** HELP START ***//*

Macro:    forest_plot

Purpose:  
  Create a forest plot using PROC SGPLOT.
  The macro draws point estimates and confidence intervals and displays
  up to six descriptive columns on the left side using YAXISTABLE.
  Optional reference line and code generation (mprint dump) are supported.

Parameters:  
  data            Input dataset  
                  (default: dummy_forest_test)

  out1-out6       Variable names to be displayed on the left side
                  using YAXISTABLE (optional)

  marker_point    Variable for point estimate
                  (default: estimate)

  bar_left        Variable for lower confidence limit
                  (default: lower_limit)

  bar_right       Variable for upper confidence limit
                  (default: upper_limit)

  out1_label      Label for out1 column
  out2_label      Label for out2 column
  out3_label      Label for out3 column
  out4_label      Label for out4 column
  out5_label      Label for out5 column
  out6_label      Label for out6 column

  AxisValues      X-axis values specification passed to
                  XAXIS VALUES=()
                  (default: 0.0 to 2.5 by 0.5)

  refline_value   Reference line value on X-axis
                  (default: 1)

  bar_color       Color of confidence interval bars
                  (default: black)

  marker_color    Color of marker points
                  (default: black)

  Generate_Code   Output expanded macro code (mprint) to a text file
                  in the WORK directory and open it
                  (Y/N, default: N)

Output:  
  - Forest plot generated by ODS Graphics
  - WORK.wk_forest      : plotting dataset
  - WORK.forest_anno    : SG annotation dataset
  - forest_plot<index>.txt (when Generate_Code=Y)

Usage Example:  
  %forest_plot(
    data=dummy_forest_test,
    out1=col1,
    out2=col2,
    out3=col3,
    out4=col4,
    out5=col5,
    marker_point=estimate,
    bar_left=lower_limit,
    bar_right=upper_limit,
    out1_label=%nrbquote(Sub Group),
    out2_label=%nrbquote(n),
    out3_label=%nrbquote(%),
    out4_label=%nrbquote(HR),
    out5_label=%nrbquote(HR 95 %CL),
    AxisValues=%nrbquote(0.0 to 2.5 by 0.5),
    refline_value=1,
    bar_color=black,
    marker_color=black,
    Generate_Code=Y
  );

*//*** HELP END ***/

%macro forest_plot(
data = dummy_forest_test,
out1=col1,
out2=,
out3=,
out4=,
out5=,
out6=,
marker_point=estimate,
bar_left=lower_limit,
bar_right=upper_limit,
out1_label=%nrbquote(Sub Group) ,
out2_label= ,
out3_label=,
out4_label=,
out5_label=,
out6_label=,
AxisValues =%nrstr (0.0 to 2.5 by 0.5),
refline_value =1,
bar_color=black,
marker_color=black,
Generate_Code =N
);
%let codepath = %sysfunc(pathname(WORK));
%put &codepath;
options nomfile;
%if %upcase(&Generate_Code) =Y %then %do;
  %let ind=&sysindex.;
  filename mprint "&codepath.\forest_plot&ind..txt";
  options mfile mprint;
%end;

filename mytemp temp;
data _null_;
file mytemp;
put"Age@@@@@@@";
put"< 20@99@(99.9)@99.99@[99.99-99.99]@1.05@1.5@1.9";
put"20 - < 30@99@(99.9)@99.99@[99.99-99.99]@0.4@0.7@1.15";
put"30 - < 40@99@(99.9)@99.99@[99.99-99.99]@0.6@0.8@1.25";
put"Sex@@@@@@@";
put"Male@99@(99.9)@99.99@[99.99-99.99]@1.2@1.5@1.8";
put"Female@99@(99.9)@99.99@[99.99-99.99]@0.6@0.8@1";
put"Race or ethnic group@@@@@@@";
put"AAA@99@(99.9)@99.99@[99.99-99.99]@0.6@1.05@1.2";
put"BBB@99@(99.9)@99.99@[99.99-99.99]@0.85@1.2@1.6";
put"From XX to Randomization@@@@@@@";
put"<= 7 days@99@(99.9)@99.99@[99.99-99.99]@0.8@1.2@1.5";
put"> 7 days@99@(99.9)@99.99@[99.99-99.99]@0.75@1.15@1.5";
put"COMPLICATION@@@@@@@";
put"Yes@99@(99.9)@99.99@[99.99-99.99]@0.9@1.4@2";
put"No@99@(99.9)@99.99@[99.99-99.99]@0.8@1.1@1.5";
run;
data dummy_forest_test;
attrib
col1 length=$25. col2 length=$2. col3 length=$6. col4 length=$5. col5 length=$13. lower_limit length=8. estimate length=8. upper_limit length=8.;
infile mytemp lrecl=3000  dlm='@' dsd;
input col1 col2 col3 col4 col5 lower_limit estimate upper_limit ;
run;
filename mytemp clear;

data wk_forest;
set &data.;

%if %length(&out1_label.) > 0 %then %do; label out1="&out1_label.";label gap="&out1_label.";%end;
%if %length(&out2_label.) > 0 %then %do; label out2="&out2_label.";%end;
%if %length(&out3_label.) > 0 %then %do; label out3="&out3_label.";%end;
%if %length(&out4_label.) > 0 %then %do; label out4="&out4_label.";%end;
%if %length(&out5_label.) > 0 %then %do; label out5="&out5_label.";%end;
%if %length(&out6_label.) > 0 %then %do; label out6="&out6_label.";%end;
;
%if %length(&out1.) > 0 %then %do;out1=&out1;%end;
%if %length(&out2.) > 0 %then %do;out2=&out2;%end;
%if %length(&out3.) > 0 %then %do;out3=&out3;%end;
%if %length(&out4.) > 0 %then %do;out4=&out4;%end;
%if %length(&out5.) > 0 %then %do;out5=&out5;%end;
%if %length(&out6.) > 0 %then %do;out6=&out6;%end;
bar_right=&bar_right.;
bar_left=&bar_left.;
marker_point=&marker_point.;
ObsId=_n_; 
gap=out1;
indentWt=10;
call missing(of gap);
run;
%sganno
data forest_anno;
set wk_forest(keep=out1 ObsId marker_point);
indent=1.2;
if ^missing(marker_point) then indent=2;
%sgtext(label=out1 , y1space="DATAVALUE", x1space="GRAPHPERCENT",textcolor="black",width=1000,x1=indent,y1=ObsId,justify="LEFT",textsize=8,ANCHOR="LEFT" ) ;
run;
ods graphics / reset noborder noscale
               width=800px height=580px imagefmt=png;
proc sgplot data=wk_forest nowall noborder nocycleattrs noautolegend sganno = forest_anno;
  styleattrs axisextent=data;
  highlow y=obsid low=bar_left high=bar_right /highcap=serif lowcap=serif lineattrs=(color=&bar_color) ;
  scatter y=obsid x=marker_point / markerattrs=(symbol=squarefilled color=&marker_color);
  %if %length(&refline_value) ne 0 %then %do;refline &refline_value / axis=x lineattrs=(color=gray);%end;
  yaxistable gap  / location=inside position=left labelattrs=(size=9 ) labeljustify=left valueattrs=(size=8) indentweight=indentWt;
  %if %length(&out2.) > 0 %then %do;yaxistable out2 / location=inside position=left labelattrs=(size=9 ) valueattrs=(size=8);%end;
  %if %length(&out3.) > 0 %then %do;yaxistable out3 / location=inside position=left labelattrs=(size=9 ) valueattrs=(size=8);%end;
  %if %length(&out4.) > 0 %then %do;yaxistable out4 / location=inside position=left labelattrs=(size=9 ) valueattrs=(size=8);%end;
  %if %length(&out5.) > 0 %then %do;yaxistable out5 / location=inside position=left labelattrs=(size=9 ) valueattrs=(size=8);%end;
  %if %length(&out6.) > 0 %then %do;yaxistable out6 / location=inside position=left labelattrs=(size=9 ) valueattrs=(size=8);%end;
  yaxis reverse display=none  offsetmin=0.05 offsetmax=0.05;
  xaxis display=(nolabel) values=(&AxisValues.) valueattrs=(size=7);
run;

%if %upcase(&Generate_Code) =Y %then %do;
%*-- Only for Windows system --*;
%if %index(%upcase(&SYSSCP), WIN) > 0 %then %do;
options noxwait noxsync;
%end;
  options nomprint nomfile;
  filename mprint clear;
  data _null_;
  	call sleep(1,1);
  run;
%if %sysfunc(getoption(xcmd))=XCMD %then  %sysexec "&codepath.\forest_plot&ind..txt";
%end;

%mend forest_plot;
