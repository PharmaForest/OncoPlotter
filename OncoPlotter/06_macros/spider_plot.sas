/*** HELP START ***//*


Macro:    spider_plot

Purpose:  
  Create a spider (subject profile) plot using PROC SGPLOT.
  The macro draws a series line (with markers) for each subject across
  the specified X variable (e.g., study day) and Y variable (e.g., percent change).
  Optional reference lines, custom axis labels/values, and code generation
  (mprint dump) are supported.
  A dummy input dataset is created within the macro for demonstration.

Parameters:  
  data               Input dataset  
                     (default: dummy_spider)

  xvar               X variable for the horizontal axis
                     (default: ADY)

  yvar               Y variable for the vertical axis
                     (default: PCHG)

  subject_var        Subject identifier used as GROUP= for the series
                     (default: SUBJID)

  subject_category   Optional category variable intended for color control
                     via GROUPLC=/GROUPMC= in the SERIES statement
                     (default: BOR)

  xaxis_label        X-axis label
                     (default: Days)

  xaxis_values       X-axis values specification passed to
                     XAXIS VALUES=()
                     (default: 0 43 85 127 169 191 213 235)

  yaxis_label        Y-axis label
                     (default: Change rate from baseline (%))

  yaxis_values       Y-axis values specification passed to
                     YAXIS VALUES=()
                     (default: -100 -75 -50 -30 0 20 50 75 100)

  datacontrastcolors Color list for group lines/markers passed to
                     STYLEATTRS DATACONTRASTCOLORS=()
                     (default: Blue Red Green Yellow)

  refline_value      Reference line value(s) on Y-axis
                     (default: 0 20 -30)

  curvelabel         Display curve labels on the series lines
                     (Y/N, default: Y)

  Generate_Code      Output expanded macro code (mprint) to a text file
                     in the WORK directory and open it
                     (Y/N, default: N)

Output:  
  - Spider plot generated by ODS Graphics
  - WORK.dummy_spider : demonstration dataset created within the macro
  - spider_plot<index>.txt (when Generate_Code=Y)

Usage Example:  
  %spider_plot(
    data = dummy_spider,
    xvar = ADY,
    yvar = PCHG,
    subject_var = SUBJID,
    subject_category = BOR,
    xaxis_label = %nrbquote(Days),
    xaxis_values = %nrbquote(0 43 85 127 169 191 213 235),
    yaxis_label = %nrbquote(Change rate from baseline (%)),
    yaxis_values = %nrbquote(-100 -75 -50 -30 0 20 50 75 100),
    datacontrastcolors = %nrbquote(Blue Red Green Yellow),
    refline_value = 0 20 -30,
    curvelabel = Y,
    Generate_Code = N
  );

*//*** HELP END ***/

%macro spider_plot(
data = dummy_spider,
xvar=ADY,
yvar=PCHG,
subject_var=SUBJID,
subject_category=BOR,
xaxis_label=%nrbquote(Days) ,
xaxis_values =%nrbquote (0 43 85 127 169 191 213 235),
yaxis_label=%nrbquote(Change rate from baseline (%)) ,
yaxis_values =%nrbquote(-100 -75 -50 -30  0 20 50 75 100),
datacontrastcolors=%nrbquote(Blue Red Green Yellow),
refline_value =0 20 -30,
curvelabel=Y,
Generate_Code =N
);
%let codepath = %sysfunc(pathname(WORK));
%put &codepath;
options nomfile;
%if %upcase(&Generate_Code) =Y %then %do;
  %let ind=&sysindex.;
  filename mprint "&codepath.\spider_plot&ind..txt";
  options mfile mprint;
%end;

filename mytemp temp;
data _null_;
file mytemp;
put"101810@0@0@Complete Response";
put"101810@43@-10@Complete Response";
put"101810@85@-60@Complete Response";
put"101810@127@-70@Complete Response";
put"101810@169@-88@Complete Response";
put"101810@191@-100@Complete Response";
put"103101@0@0@Progressive Disease";
put"103101@43@10@Progressive Disease";
put"103101@85@50@Progressive Disease";
put"103101@127@70@Progressive Disease";
put"103105@0@0@Progressive Disease";
put"103105@43@10@Progressive Disease";
put"103105@85@30@Progressive Disease";
put"103105@127@40@Progressive Disease";
put"103105@169@45@Progressive Disease";
put"103201@0@0@Partial Response";
put"103201@43@-10@Partial Response";
put"103201@85@-10@Partial Response";
put"103201@127@-30@Partial Response";
put"103201@169@-20@Partial Response";
put"103201@191@-40@Partial Response";
put"103205@0@0@Partial Response";
put"103205@43@-30@Partial Response";
put"103205@85@-40@Partial Response";
put"103205@127@-50@Partial Response";
put"103205@169@-55@Partial Response";
put"103205@191@-55@Partial Response";
put"103205@213@-40@Partial Response";
run;

data dummy_spider;
attrib SUBJID length=$8. ADY length=8. PCHG length=8. BOR length=$19.;
infile mytemp lrecl=3000  dlm='@' dsd;
input SUBJID ADY PCHG BOR ;
run;
filename mytemp clear;

ods graphics / reset noborder noscale
               width=700px height=400px imagefmt=png;
proc sgplot data=&data. noborder ;
 styleattrs datacontrastcolors=(&datacontrastcolors.);
 %if %length(&refline_value) ne 0 %then %do;refline &refline_value. / lineattrs=(pattern=shortdash);%end;
  series x=&xvar. y=&yvar. / group=&subject_var 
         %if %length(&subject_category.) ne 0 %then %do; grouplc=&subject_category. groupmc=&subject_category. %end;
         markers markerattrs=(symbol=circlefilled) 
         lineattrs=(thickness=2 pattern=solid) name='spider' 
         %if %upcase(&curvelabel) = Y %then %do;curvelabel %end;; 
   keylegend 'spider' / title='' noborder type=linecolor valueattrs=(size=10) 
         location=inside position=topright across=1 opaque;
  xaxis label="&xaxis_label." values=(&xaxis_values.);
  yaxis label="&yaxis_label." values=(&yaxis_values.) ;
run;

%if %upcase(&Generate_Code) =Y %then %do;
%*-- Only for Windows system --*;
%if %index(%upcase(&SYSSCP), WIN) > 0 %then %do;
options noxwait noxsync;
%end;
  options nomprint nomfile;
  filename mprint clear;
  data _null_;
  	call sleep(1,1);
  run;
%if %sysfunc(getoption(xcmd))=XCMD %then  %sysexec "&codepath.\spider_plot&ind..txt";
%end;
%mend spider_plot;
